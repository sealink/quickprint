plugins {
    id 'org.springframework.boot' version '2.6.6'
    id 'io.spring.dependency-management' version '1.1.0'
    id 'org.jetbrains.kotlin.jvm' version '1.8.21'
    id 'org.jetbrains.kotlin.plugin.spring' version '1.8.21'
    id 'org.jetbrains.kotlin.plugin.allopen' version '1.8.21'
    id 'war'
    id 'maven-publish'
    id 'net.nemerosa.versioning' version '3.0.0'
    id 'jacoco'
    id 'com.github.kt3k.coveralls' version '2.12.2'
    id 'com.bmuschko.docker-remote-api' version '9.3.1'
    id 'com.patdouble.awsecr' version '0.7.0'
}

jacocoTestReport {
    reports {
        xml.required.set(true) // coveralls plugin depends on xml format report
        html.required.set(true)
    }
}

test {
    useJUnitPlatform()

    testLogging {
        events "failed"
        exceptionFormat "full"
    }
}

group = 'au.com.sealink'
sourceCompatibility = 19
compileKotlin {
	kotlinOptions {
		freeCompilerArgs = ["-Xjsr305=strict"]
		jvmTarget = "19"
	}
}
compileTestKotlin {
	kotlinOptions {
		freeCompilerArgs = ["-Xjsr305=strict"]
		jvmTarget = "19"
	}
}

versioning {
    branchEnv = ['TRAVIS_BRANCH', 'TRAVIS_TAG', 'TRAVIS_PULL_REQUEST_BRANCH', 'GIT_BRANCH']
}

if(versioning.info.tag == null) {
    version = versioning.info.display
}
else {
    version = versioning.info.tag
}

repositories {
    google()
    jcenter()
    mavenCentral()
    maven {
        name = "GitHubPackages"
        url = "https://maven.pkg.github.com/sealink/ticket_printer"
        credentials {
            username = System.getenv("GITHUB_USERNAME")
            password = System.getenv("GITHUB_TOKEN")
        }
    }
}

bootJar {
    launchScript()
}

task createProperties(dependsOn: processResources) {
    doLast {
        new File("$buildDir/resources/main/version.properties").withWriter { w ->
            Properties p = new Properties()
            p['version'] = project.version.toString()
            p.store w, null
        }
    }
}

classes {
    dependsOn createProperties
}

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier.set('sources')
    from sourceSets.main.allSource
}

artifacts {
    archives sourcesJar, bootWar
}

configurations {
    runtimeClasspath {
        exclude group: 'org.springframework.boot', module: 'spring-boot-devtools'
    }
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation "org.springframework.boot:spring-boot-starter-security"
    implementation "org.springframework.boot:spring-boot-starter-actuator"
    implementation group: 'org.springframework.data', name: 'spring-data-commons', version: '2.5.1'
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.4.3-native-mt"
		implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
		implementation "org.jetbrains.kotlin:kotlin-reflect"
    implementation 'com.fasterxml.jackson.module:jackson-module-kotlin'
    implementation 'au.com.sealink:printing:1.9.0'


    implementation group: 'com.rapid7', name: 'r7insight_java', version: '3.0.7'
    testImplementation('org.springframework.boot:spring-boot-starter-test') {
			exclude module: 'junit'
            exclude module: 'junit-vintage-engine'
		}
    testImplementation("com.nhaarman.mockitokotlin2:mockito-kotlin:2.2.0")
		testImplementation 'org.junit.jupiter:junit-jupiter:5.8.1'
    providedRuntime 'org.springframework.boot:spring-boot-starter-tomcat'
}

def pomConfig = {
    licenses {
        license {
            name "MIT License"
            url "http://www.opensource.org/licenses/mit-license.php"
            distribution "repo"
        }
    }
    developers {
        developer {
            id "jmortlock"
            name "John Mortlock"
            email "john.mortlock@sealink.com.au"
        }
    }

    scm {
        url "https://github.com/sealink/quickprint.git"
    }
}

publishing {
    publications {
        mavenPublication(MavenPublication) {
            from components.web
            artifact sourcesJar
            groupId 'au.com.sealink'
            artifactId 'quickprint'
            version project.version
            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'SeaLink Printing Service')
                root.appendNode('name', 'SeaLink Printing Service')
                root.appendNode('url', 'https://github.com/sealink/quickprint')
                root.children().last() + pomConfig
            }
        }
    }
    repositories {
        maven {
            name = "GitHubPackages"
            url = "https://maven.pkg.github.com/sealink/quickprint"
            credentials {
                username = System.getenv("GITHUB_USERNAME")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
}

def ecrBase = '120983864185.dkr.ecr.ap-southeast-2.amazonaws.com'

docker {
    registryCredentials {
        url = "https://$ecrBase"
    }
}

import com.bmuschko.gradle.docker.tasks.image.Dockerfile

task createDockerfile(type: Dockerfile) {
    from 'openjdk:19-jdk-buster'
    label(
    	'maintainer': 'john.mortlock@sealink.com.au'
    )
    environmentVariable 'PRINTER_NAME', 'cups-pdf'
    volume '/root/PDF'
    runCommand 'apt-get update && apt-get -y install cups-pdf cups-bsd && apt-get clean'
    copyFile 'cups-pdf.conf', '/etc/cups/cups-pdf.conf'
    copyFile war.archiveFileName.get(), '/app/quickprint.war'
    copyFile 'entrypoint.sh', '/app/entrypoint.sh'
    exposePort 8080
    entryPoint '/app/entrypoint.sh'
}

task syncWebAppArchive(type: Sync) {
    dependsOn assemble
    from war.archivePath
    into createDockerfile.getDestDir()

    from project.file('docker/entrypoint.sh')
    into createDockerfile.getDestDir()

    from project.file('docker/cups-pdf.conf')
    into createDockerfile.getDestDir()
}

createDockerfile.dependsOn syncWebAppArchive

import com.bmuschko.gradle.docker.tasks.image.DockerBuildImage

def ecrImageTag = "${ecrBase}/quickprint:${version}"
task buildImage(type: DockerBuildImage) {
    dependsOn createDockerfile
    inputDir = createDockerfile.getDestDir()
    images.add(ecrImageTag)
}

import com.bmuschko.gradle.docker.tasks.image.DockerPushImage

task pushImage(type: DockerPushImage) {
    dependsOn buildImage
    getImages().add(ecrImageTag)
}

task publishImage() {
    dependsOn pushImage
}

task createDockerrun() {
    file('Dockerrun.aws.json').text = """{
  "AWSEBDockerrunVersion": "1",
  "Image": {
    "Name": "${ecrImageTag}",
    "Update": "true"
  },
  "Ports": [
    {
      "ContainerPort": "8080"
    }
  ],
  "Volumes": [
    {
      "HostDirectory": "/mnt/nfs/PDF",
      "ContainerDirectory": "/root/PDF"
    }
  ],
  "Logging": "/var/log/nginx"
}
"""
}

task copyEBDirForArchiving(type: Copy) {
    dependsOn createDockerrun
    from(".") {
        include ".ebextensions/**"
    }

    from "Dockerrun.aws.json"
    into "${buildDir}/toArchive"
}

task buildEBArchive(type: Zip) {
    dependsOn copyEBDirForArchiving
    from "${buildDir}/toArchive"
    archiveFileName.set('eb.zip')
}
